# CMake Version
CMAKE_MINIMUM_REQUIRED(VERSION 2.8 FATAL_ERROR)

# CMake Include
INCLUDE(CheckCSourceRuns)
INCLUDE(ExternalProject)

# Build Type
SET(CMAKE_BUILD_TYPE Debug)
# Build Type Option
SET(CMAKE_C_FLAGS_RELEASE "-Wall -O2")
SET(CMAKE_C_FLAGS_DEBUG "-W -g")

# Project
PROJECT(udf_zmq)
SET(MAJOR_VERSION 0)
SET(MINOR_VERSION 1)
SET(BUILD_VERSION 0)
#SET(REVISION_VERSION 0)

# MySQL header
FIND_PATH(MYSQL_INCLUDES
  mysql.h
  PATHS ${MYSQL_INCLUDE_PATH} /usr/include /usr/include/mysql)
IF(MYSQL_INCLUDES STREQUAL "MYSQL_INCLUDES-NOTFOUND")
  MESSAGE(FATAL_ERROR "mysql header could not found mysql.h\n"
    "OPTION: -DMYSQL_INCLUDE_PATH=path")
ENDIF()
INCLUDE_DIRECTORIES(${MYSQL_INCLUDES})

# Jansson
MESSAGE(STATUS "Use Jansson library")
ExternalProject_ADD(
  jansson
  SOURCE_DIR ${PROJECT_SOURCE_DIR}/third_party/jansson
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}/deps
  CMAKE_ARGS
  -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
  -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
  -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
  -DJANSSON_BUILD_DOCS=OFF
  ${USE_PROJECT_CMAKE_MODULE_PATH})
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR}/deps/include)
SET(JANSSON_LIBRARIES ${CMAKE_CURRENT_BINARY_DIR}/deps/lib/libjansson.a)
SET(DEPS jansson)

# ZeroMQ (>= 4.0)
SET(ZEROMQ_MINIMUM_REQUIRED_VERSION 40000)
FIND_PATH(ZEROMQ_INCLUDES
  zmq.h
  PATHS ${ZEROMQ_INCLUDE_PATH} /usr/include)
IF(ZEROMQ_INCLUDES STREQUAL "ZEROMQ_INCLUDES-NOTFOUND")
  MESSAGE(FATAL_ERROR "ZeroMQ could not found zmq.h\n"
    "OPTION: -DZEROMQ_INCLUDE_PATH=path")
ENDIF()
INCLUDE_DIRECTORIES(${ZEROMQ_INCLUDES})

FIND_LIBRARY(ZEROMQ_LIBRARIES
  NAMES zmq
  PATHS ${ZEROMQ_LIBRARY_PATH} /usr/lib64 /usr/lib)
IF(ZEROMQ_LIBRARIES STREQUAL "ZEROMQ_LIBRARIES-NOTFOUND")
  MESSAGE(FATAL_ERROR "ZeroMQ could not found libzmq.so\n"
    "OPTION: -DZEROMQ_LIBRARY_PATH=path")
ENDIF()

CHECK_C_SOURCE_RUNS("
    #include <zmq.h>
    int main() {
        if (ZMQ_VERSION > ${ZEROMQ_MINIMUM_REQUIRED_VERSION}) {
            return 0;
        } else {
            return 1;
        }
    }" HAVE_ZMQ_VERSION)
IF(NOT HAVE_ZMQ_VERSION)
  MESSAGE(FATAL_ERROR "ZeroMQ 4.0 or higher is required.")
ENDIF()

# Library
SET(LIBNAME udf_zmq)
ADD_LIBRARY(${LIBNAME} SHARED ${PROJECT_SOURCE_DIR}/src/main.c)
SET_TARGET_PROPERTIES(${LIBNAME} PROPERTIES PREFIX "")
TARGET_LINK_LIBRARIES(${LIBNAME} ${ZEROMQ_LIBRARIES} ${JANSSON_LIBRARIES})
ADD_DEPENDENCIES(${LIBNAME} ${DEPS})

# MySQL config
FIND_PROGRAM(MYSQL_CONFIG mysql_config
  ${MYSQL_CONFIG_PATH} /usr/bin/ /usr/local/bin/)

IF(MYSQL_CONFIG)
  EXEC_PROGRAM(${MYSQL_CONFIG} ARGS --plugindir OUTPUT_VARIABLE MYSQL_PLUGIN_DIR)
  MESSAGE(STATUS "Using mysql-config: ${MYSQL_PLUGIN_DIR}")

  # Install
  INSTALL(TARGETS ${LIBNAME} DESTINATION ${MYSQL_PLUGIN_DIR})
ENDIF()
